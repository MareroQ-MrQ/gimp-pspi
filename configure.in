dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_CONFIG_HEADER(config.h)

PLUGIN_MAJOR_VERSION=1
PLUGIN_MINOR_VERSION=0
PLUGIN_MICRO_VERSION=0
PLUGIN_VERSION=$PLUGIN_MAJOR_VERSION.$PLUGIN_MINOR_VERSION.$PLUGIN_MICRO_VERSION

VERSION=$PLUGIN_VERSION

PACKAGE_NAME=gimp-pspi
PLUGIN_NAME=pspi

AM_INIT_AUTOMAKE($PACKAGE_NAME, $VERSION)

AC_MSG_CHECKING([for native Win32])
case "$host" in
  *-*-mingw*)
    os_win32=yes
    ;;
  *)
    os_win32=no
    ;;
esac
AC_MSG_RESULT([$os_win32])
AM_CONDITIONAL(OS_WIN32, test "$os_win32" = "yes")

AC_ARG_WITH(pssdk, [  --with-pssdk=DIRECTORY location of the Photoshop SDK],
	    [PSSDK=$with_pssdk],
	    [AC_MSG_ERROR([You must use the --with-pssdk flag])])

AC_SUBST(PSSDK)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AM_MAINTAINER_MODE

PKG_CHECK_MODULES(GTK, gtk+-1.3-win32-production >= 1.3.0)
PKG_CHECK_MODULES(GLIB, glib-2.0 >= 1.3.9)

AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

AM_PATH_GIMP(1.2.1, ,
            AC_MSG_ERROR(Cannot find GIMP: Is gimptool in path?))

dnl For Debian, all directories must be expressed in terms of prefix
GIMP_PLUGIN_DIR=`gimptool --gimpplugindir | sed -e "s/$( gimptool --prefix | sed 's/\//\\\\\\//g')/\$\{prefix\}/"`

dnl Add the languages which your application supports here.
ALL_LINGUAS="fi"
AM_GLIB_GNU_GETTEXT

LIBS="$LIBS $INTLLIBS"

GETTEXT_PACKAGE=$PACKAGE_NAME
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

LOCALEDIR='${prefix}/${DATADIRNAME}/locale'

DATADIR='${prefix}/${DATADIRNAME}/${PLUGIN_NAME}'

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

CPPFLAGS="$CPPFLAGS -DGTK_DISABLE_COMPAT_H"

AC_SUBST(PLUGIN_MAJOR_VERSION)
AC_SUBST(PLUGIN_MINOR_VERSION)
AC_SUBST(PLUGIN_MICRO_VERSION)
AC_SUBST(PLUGIN_VERSION)

AC_SUBST(PLUGIN_NAME)

AC_SUBST(LOCALEDIR)
AC_SUBST(DATADIR)

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
])
